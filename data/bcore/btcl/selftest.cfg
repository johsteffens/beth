/** BTCL selftest
 *  Returns number of failed tests.
 */

(

test = func( name, ok )
{
    if( !ok ) { ?(name+" failed \n") };
    ok
};

err = 0;

err = err + !test( "arithmetic", 5 + 3 * 2 == 11 );
err = err + !test( "negation", -( 5 + 3 * 2 ) == 0-11 );
err = err + !test( "variable", x = 5; x + 3 * 2 == 11 );
err = err + !test( "string1", x = 12; x + "ab" + x == "12ab12" );
err = err + !test( "string2", x = 12; x + x + "ab" == "24ab" );
err = err + !test( "prefix priority", x = func(){5}; -x()*2 == -10 );

/// factorial function
factorial = func( a )
{
    if( a <= 1 )
    {
        1.0
    }
    else
    {
        a * self( a - 1 )
    }
};

err = err + !test( "factorial", factorial( 4 ) == 24 );

/// ackermann function
ackermann = func( n, m )
{
    if( n == 0 )
    {
        m + 1
    }
    else
    {
        if( m == 0 )
        {
            self( n - 1, 1 )
        }
        else
        {
            self( n - 1, self( n, m - 1 ) )
        }
    }
};

err = err + !test( "ackermann", ackermann( 3, 1 ) == 13 );

err

)
